// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        room_version = '2.4.1'
        kotlin_version = '1.5.21'
        koin_version= '3.1.2'
        lifecycle_version = '2.4.0'
        retrofit_version = '2.9.0'
        coroutines_version = '1.5.2'
        appcompat_version = '1.4.1'
        material_version = '1.5.0'
        constraintlayout_version = '2.1.3'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

//For all subprojects - generate compileSdkVersion, minSdkVersion, targetSdkVersion and Java compatibility
subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {
            android {
                compileSdk 31

                buildFeatures {
                    viewBinding true
                }

                defaultConfig {
                    minSdk 21
                    targetSdk 31
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}